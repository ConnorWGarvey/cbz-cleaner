#!/usr/bin/env groovy

import groovy.transform.ToString
import java.nio.file.Files
import java.nio.file.LinkOption
import java.nio.file.Path
import java.nio.file.Paths
import java.nio.file.attribute.GroupPrincipal
import java.nio.file.attribute.PosixFileAttributes
import java.nio.file.attribute.PosixFileAttributeView
import java.text.DecimalFormat

class Combiner {
  void apply(Map opts=[:], Path parentDirectory) {
    def directories = parentDirectory.listSorted().findAll{it.isDirectory()}
    if (directories.isEmpty()) return
    def volumeList = directories.findAll{it.name.contains(' - Volume ')}.collect{Volume.parse(it)}.findAll{it}
    volumeList.forEach { volume ->
      if ((volume) == null) {
        throw new ExitException("Error parsing volume ${directory.name}")
      }
    }
    def volumes = volumeList.groupBy{it.series}
    println 'Moving images'
    directories.findAll{!it.name.contains(' - Volume ') && it.hasFiles()}.each { directory ->
      clean(directory)
      def match = (directory.name =~ /(.*) - (\d{4}(?:\.\d+)?)$/)[0]
      def series = match[1]
      def issue = match[2] as BigDecimal
      def volume = volumes[series].find{it.hasIssue(issue)}
      if (volume == null) {
        throw new ExitException("Couldn't find a volume for ${directory.name} : $series from $volumes")
      }
      println "  Moving $issue from $series"
      renumber(directory:directory, issue:issue)
      mergeDirectory(destination:volume.directory, source:directory)
    }
    convertAll(directory:parentDirectory, compression:opts.compression)
  }

  void convert(Map opts) {
    def sourceDirectory = opts.directory
    if (!sourceDirectory.hasFiles()) {
      println "  Not converting $sourceDirectory.name because it does't contain any files"
      return
    }
    println "  Converting $sourceDirectory.name"
    def compression = opts.compression
    def targetDirectory = sourceDirectory.parent.resolve("${sourceDirectory.fileName}-$compression")
    targetDirectory.createDirectories()
    sourceDirectory.copyPermissionsTo(targetDirectory)
    def aSourceFile = sourceDirectory.list()[0]
    def sourcePaths = Files.list(sourceDirectory).toList().toSorted()
    def digits = sourcePaths.size().toString().length()
    sourcePaths.eachWithIndex { sourcePath, index ->
      def formattedIndex = String.format("%0${digits}d", index+1)
      def targetFileName = "${formattedIndex}.$compression"
      def targetPath = targetDirectory.resolve(targetFileName)
      if (!targetPath.exists()) {
        println "    Trimming ${sourcePath.fileName} to $targetFileName"
        execute(['convert', '-fuzz', '7%', '-trim', sourcePath.toString(), targetPath.toString()])
        aSourceFile.copyPermissionsTo(targetPath)
      }
    }
    targetDirectory.eachFile{aSourceFile.copyPermissionsTo(it)}
    sourceDirectory.deleteDir()
    targetDirectory.moveTo(sourceDirectory)
  }

  void convertAll(Map opts) {
    println 'Converting images'
    opts.directory.listSorted().findAll{it.isDirectory()}.each { directory ->
      convert(compression:opts.compression, directory:directory)
    }
  }

  void clean(Path directory) {
    def nomedia = directory.resolve('.nomedia')
    if (nomedia.exists()) nomedia.delete()
  }

  void execute(List<String> command) {
    def proc = command.execute()
    def out = proc.in.text
    def err = proc.err.text
    proc.waitFor()
    def exitValue = proc.exitValue()
    if (exitValue != 0) {
      throw new ExitException("$out\n\n$err")
    }
  }

  void mergeDirectory(Map opts) {
    opts.source.eachFile { file ->
      def target = opts.destination.resolve(file.name)
      if (target.exists()) throw new ExitException("Can't create $file because it exists")
      file.moveTo(target)
    }
    opts.source.delete()
  }

  void renumber(Map opts) {
    def issue = opts.issue.zeroPad(6)
    opts.directory.eachFileOrderedWithIndex { path, index ->
      path.renameTo("${issue}-${(index+1).zeroPad(6)}.${path.extension}")
    }
  }
}

class ExitException extends Exception {
  public ExitException(String message) {
    super(message)
  }
}

@ToString(includeNames=true) class Volume {
  Path directory
  BigDecimal end
  int number
  String series
  BigDecimal start

  boolean hasIssue(BigDecimal issue) {
    issue >= start && issue <= end
  }

  static Volume parse(Path path) {
    def match = path.name =~ /(.*) - (\d{4}(?:\.\d+)?)-(\d{4}(?:\.\d+)?) - Volume (\d{2})/
    if (match.size() == 1) {
      def group = match[0]
      return new Volume(directory:path, end:group[3] as BigDecimal, number:group[4] as Integer, series:group[1], start:group[2] as BigDecimal)
    } else return null
  }
}

BigDecimal.metaClass.zeroPad = {int amount -> new DecimalFormat("${'0'*amount}.######").format(delegate)}
Integer.metaClass.zeroPad = {int amount -> String.format("%0${amount}d", delegate)}
Path.metaClass.copyPermissionsTo = { Path to ->
  GroupPrincipal group = Files.readAttributes(delegate, PosixFileAttributes.class, LinkOption.NOFOLLOW_LINKS).group();
  Files.getFileAttributeView(to, PosixFileAttributeView.class, LinkOption.NOFOLLOW_LINKS).setGroup(group);
  to.writePosixFilePermissions(delegate.readPosixFilePermissions())
}
Path.metaClass.createDirectories = {Files.createDirectories(delegate)}
Path.metaClass.delete = {Files.delete(delegate)}
Path.metaClass.eachFileOrdered = {Closure closure -> delegate.listSorted().each{closure.call(it)}}
Path.metaClass.eachFileOrderedWithIndex = {Closure closure ->
  delegate.listSorted().eachWithIndex{item, index -> closure.call(item, index)}
}
Path.metaClass.exists = {Files.exists(delegate)}
Path.metaClass.getExtension = {delegate.name[delegate.name.lastIndexOf('.')+1..-1]}
Path.metaClass.getName = {
  def name = delegate.toString()
  name[name.lastIndexOf('/')+1..-1]
}
Path.metaClass.getNameWithoutExtension = {delegate.name[0..delegate.name.lastIndexOf('.')-1]}
Path.metaClass.hasFiles = {delegate.list().any{it.isFile()}}
Path.metaClass.isDirectory = {Files.isDirectory(delegate)}
Path.metaClass.isFile = {Files.isRegularFile(delegate)}
Path.metaClass.list = {Files.list(delegate).toList()}
Path.metaClass.listFiles = {delegate.list().filter{it.isFile()}}
Path.metaClass.listSorted = {delegate.list().toSorted()}
Path.metaClass.moveTo = {Path destination -> Files.move(delegate, destination)}
Path.metaClass.readPosixFilePermissions = {Files.getPosixFilePermissions(delegate)}
Path.metaClass.renameTo = { String newName ->
  def destination = delegate.parent.resolve(newName)
  delegate.moveTo(destination)
}
Path.metaClass.writePosixFilePermissions = {permissions -> Files.setPosixFilePermissions(delegate, permissions)}
String.metaClass.toBigDecimal = {delegate as BigDecimal}
String.metaClass.toInt = {delegate as Integer}

try {
  def parser = new CliBuilder(usage:'combine-comic-volumes').tap {
    _ longOpt:'compression', args:1, defaultValue:'png', 'image compression'
  }
  def options = parser.parse(args)
  new Combiner().apply(Paths.get('').toAbsolutePath(), compression:options.compression)
} catch (ExitException e) {
  println e.message
  System.exit(1)
}

