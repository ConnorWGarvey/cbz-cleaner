#!/usr/bin/env groovy

import java.nio.file.StandardCopyOption
import java.nio.file.Files
import java.nio.file.FileSystems
import java.nio.file.LinkOption
import java.nio.file.Path
import java.nio.file.attribute.GroupPrincipal
import java.nio.file.attribute.PosixFileAttributes
import java.nio.file.attribute.PosixFileAttributeView
import java.util.zip.ZipEntry
import java.util.zip.ZipFile
import java.util.zip.ZipOutputStream

  
   
class Packager {
  static GARBAGE_DIRECTORIES = ['__MACOSX/'].toSet()
  static GARBAGE_FILES = ['.DS_Store'].toSet()
  static ARCHIVE_EXTENSIONS = ['cbz', 'zip'].toSet()

  boolean allFilesAreInOneDirectory(Path path) {
    new ZipFile(path.toFile()).withCloseable { zip ->
      def entries = sortedEntries(zip.entries())
      def directoryEntries = entries.findAll{it.isDirectory()}
      if (directoryEntries.size() != 1) return false
      def directoryEntry = directoryEntries[0]
      if (entries.any{(!it.isDirectory()) && (!it.name.startsWith(directoryEntry.name))}) return false
      true
    }
  }

  boolean anyZipEntry(Path path, Closure closure) {
    new ZipFile(path.toFile()).withCloseable { zip ->
      for (entry in sortedEntries(zip.entries())) {
        if (closure.call(entry)) return true
      }
      false
    }
  }

  void eachZipEntryIndexed(Path path, Closure closure) {
    everyZipEntryIndexed(path) { entry, size, index ->
      closure.call(entry, size, index)
      return true
    }
  }

  boolean everyZipEntry(Path path, Closure closure) {
    everyZipEntryIndexed(path) { entry, size, index -> closure.call(entry) }
  }

  boolean everyZipEntryIndexed(Path path, Closure closure) {
    new ZipFile(path.toFile()).withCloseable { zip ->
      def entries = sortedEntries(zip.entries())
      def size = zip.size()
      def index = 0
      entries.every{closure.call(it, size, index++)}
    }
  }

  /** File names are sequential numbers */
  boolean filesAreCorrectlyNumbered(Path path) {
    everyZipEntryIndexed(path) { entry, size, index ->
      def expectedName = String.format("%0${size.toString().length()}d", index+1)
      fileNameWithoutExtension(entry) == expectedName
    }
  }

  String fileName(ZipEntry entry) {
    def name = entry.name
    if (name.contains('/')) name[name.lastIndexOf('/')+1..-1]
    else name
  }

  String fileNameWithoutExtension(ZipEntry entry) {
    def name = fileName(entry)
    name[0..name.lastIndexOf('.')-1]
  }

  boolean hasGarbageDirectories(Path path) {
    anyZipEntry(path) { entry ->
      entry.isDirectory() && entry.name.toString() in GARBAGE_DIRECTORIES
    }
  }

  boolean hasGarbageFiles(Path path) {
    anyZipEntry(path) { fileName(it) in GARBAGE_FILES }
  }

  Path makeTempPath(Path file, String tag) {
    def fullFileName = file.fileName.toString()
    def fileName = fullFileName[fullFileName.lastIndexOf('/')+1..fullFileName.lastIndexOf('.')-1]
    def fileExtension = fullFileName[fullFileName.lastIndexOf('.')+1..-1]
    file.parent.resolve("$fileName-${tag}.$fileExtension")
  }
 
  void moveFilesOutOfDirectory(Path file) {
    assert Files.isRegularFile(file)
    def fileTemp = makeTempPath(file, 'nodirectory')
    new ZipFile(file.toFile()).withCloseable { zipIn ->
      new ZipOutputStream(Files.newOutputStream(fileTemp)).withCloseable { zipOut ->
        def entries = zipIn.entries()
        def directoryEntry = entries.nextElement()
        assert directoryEntry.isDirectory()
        while (entries.hasMoreElements()) {
          def entryIn = entries.nextElement()
          def entryOut = new ZipEntry(entryIn.name[entryIn.name.lastIndexOf('/')+1..-1])
          zipOut.putNextEntry(entryOut)
          zipIn.getInputStream(entryIn).withCloseable { content ->
            content.transferTo(zipOut)
          }
          zipOut.closeEntry()
        }
        zipOut.finish()
      }
    }
    GroupPrincipal group = Files.readAttributes(file.parent, PosixFileAttributes.class, LinkOption.NOFOLLOW_LINKS).group();
    Files.getFileAttributeView(fileTemp, PosixFileAttributeView.class, LinkOption.NOFOLLOW_LINKS).setGroup(group);
    //Files.setOwner(fileTemp, Files.getOwner(file.parent))
    Files.move(fileTemp, file, StandardCopyOption.REPLACE_EXISTING)
  }

  void removeGarbageDirectories(Path file) {
    assert Files.isRegularFile(file)
    def fileTemp = makeTempPath(file, 'withoutgarbagedirectories')
    new ZipFile(file.toFile()).withCloseable { zipIn ->
      new ZipOutputStream(Files.newOutputStream(fileTemp)).withCloseable { zipOut ->
        def entries = zipIn.entries().toList().toSorted{it.name}
        for (entryIn in entries) {
          if (!GARBAGE_DIRECTORIES.any{entryIn.name.toString().startsWith(it)}) {
            def entryOut = new ZipEntry(entryIn.name)
            zipOut.putNextEntry(entryOut)
            zipIn.getInputStream(entryIn).withCloseable { content ->
              content.transferTo(zipOut)
            }
          }
         zipOut.closeEntry()
        }
        zipOut.finish()
      }
    }
    GroupPrincipal group = Files.readAttributes(file.parent, PosixFileAttributes.class, LinkOption.NOFOLLOW_LINKS).group();
    Files.getFileAttributeView(fileTemp, PosixFileAttributeView.class, LinkOption.NOFOLLOW_LINKS).setGroup(group);
    //Files.setOwner(fileTemp, Files.getOwner(file.parent))
    Files.move(fileTemp, file, StandardCopyOption.REPLACE_EXISTING)
  }

  void removeGarbageFiles(Path file) {
    assert Files.isRegularFile(file)
    def fullFileName = file.fileName.toString()
    def fileName = fullFileName[fullFileName.lastIndexOf('/')+1..fullFileName.lastIndexOf('.')-1]
    def fileExtension = fullFileName[fullFileName.lastIndexOf('.')+1..-1]
    def fileTemp = file.parent.resolve("$fileName-withoutgarbagefiles.$fileExtension")
    new ZipFile(file.toFile()).withCloseable { zipIn ->
      new ZipOutputStream(Files.newOutputStream(fileTemp)).withCloseable { zipOut ->
        def entries = zipIn.entries().toList()
        for (entryIn in entries) {
          if (!(entryIn.name.toString() in GARBAGE_FILES)) {
            def entryOut = new ZipEntry(entryIn.name)
            zipOut.putNextEntry(entryOut)
            zipIn.getInputStream(entryIn).withCloseable { content ->
              content.transferTo(zipOut)
            }
          }
         zipOut.closeEntry()
        }
        zipOut.finish()
      }
    }
    GroupPrincipal group = Files.readAttributes(file.parent, PosixFileAttributes.class, LinkOption.NOFOLLOW_LINKS).group();
    Files.getFileAttributeView(fileTemp, PosixFileAttributeView.class, LinkOption.NOFOLLOW_LINKS).setGroup(group);
    //Files.setOwner(fileTemp, Files.getOwner(file.parent))
    Files.move(fileTemp, file, StandardCopyOption.REPLACE_EXISTING)
  }

  void renumberFiles(Path file) {
    assert Files.isRegularFile(file)
    def fileTemp = makeTempPath(file, 'renumbered')
    new ZipFile(file.toFile()).withCloseable { zipIn ->
      new ZipOutputStream(Files.newOutputStream(fileTemp)).withCloseable { zipOut ->
        def index = 0
        def entries = sortedEntries(zipIn.entries())
        entries.each { entryIn ->
          def oldName = entryIn.name
          def extension = oldName[oldName.lastIndexOf('.')+1..-1]
          def number = String.format("%0${zipIn.size().toString().length()}d", index+1)
          def entryOut = new ZipEntry("${number}.$extension")
          zipOut.putNextEntry(entryOut)
          zipIn.getInputStream(entryIn).withCloseable { content ->
            content.transferTo(zipOut)
          }
          zipOut.closeEntry()
          ++index
        }
        zipOut.finish()
      }
    }
    GroupPrincipal group = Files.readAttributes(file.parent, PosixFileAttributes.class, LinkOption.NOFOLLOW_LINKS).group();
    Files.getFileAttributeView(fileTemp, PosixFileAttributeView.class, LinkOption.NOFOLLOW_LINKS).setGroup(group);
    //Files.setOwner(fileTemp, Files.getOwner(file.parent))
    Files.move(fileTemp, file, StandardCopyOption.REPLACE_EXISTING)
  }

  void repackageAll(Path directory) {
    assert Files.isDirectory(directory)
    directory.eachFile { file ->
      def fileName = file.fileName.toString()
      def extension = fileName[fileName.lastIndexOf('.')+1..-1]
      if (!(extension in ARCHIVE_EXTENSIONS)) {
        return
      }
      println fileName
      if (Files.isDirectory(file)) return
      if (hasGarbageDirectories(file)) {
        println '  Cleaning garbage directories'
        removeGarbageDirectories(file)
      }
      if (allFilesAreInOneDirectory(file)) {
        println '  Moving files out of directory'
        moveFilesOutOfDirectory(file)
      }
      if (hasGarbageFiles(file)) {
        println '  Cleaning garbage files'
        removeGarbageFiles(file)
      }
      if (!filesAreCorrectlyNumbered(file)) {
        println '  Renumbering'
        renumberFiles(file)
      }
    }
  }

  List<? extends ZipEntry> sortedEntries(Enumeration<? extends ZipEntry> enumeration) {
    def entries = enumeration.toList()
    // Every file evds with a dash or underscore, then a number
    if (entries.every{!it.isDirectory()} && entries.every{fileNameWithoutExtension(it) ==~ /.*[_\-\#]\d+/}) {
      entries.
        // Group by prefix
        groupBy{(fileNameWithoutExtension(it) =~ /(.*)[_\-\#]\d+$/)[0][1]}.
        // Sort each group by postfix
        collectEntries { prefix, groupedEntries ->
          [prefix, groupedEntries.toSorted { entry ->
            def name = fileNameWithoutExtension(entry)
            def number = (name =~ /\d+$/)[0]
            number.toBigInteger()
          }]
        }.
        // Sort by prefix
        toSorted{e1, e2 -> e1.key <=> e2.key}.
        collect{it.value}.
        flatten()
    } else if (entries.every{!it.isDirectory()} && entries.every{fileNameWithoutExtension(it).isInteger()}) {
      entries.toSorted {fileNameWithoutExtension(it).toBigInteger()}
    } else {
      entries.toSorted{it.name}
    }
  }
}

new Packager().repackageAll(FileSystems.getDefault().getPath('.'))

